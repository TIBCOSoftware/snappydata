/*
 * Copyright (c) 2016 SnappyData, Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you
 * may not use this file except in compliance with the License. You
 * may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or
 * implied. See the License for the specific language governing
 * permissions and limitations under the License. See accompanying
 * LICENSE file.
 */

plugins {
  id 'com.github.johnrengelman.shadow' version '1.2.3'
  id 'de.undercouch.download' version '3.1.1'
}

apply plugin: 'scala'

compileScala.options.encoding = 'UTF-8'
// fix scala+java mix to all use compileScala which uses correct dependency order
sourceSets.main.scala.srcDir 'src/main/java'
sourceSets.test.scala.srcDirs = [ 'src/test/java', 'src/test/scala',
                                  'src/dunit/java', 'src/dunit/scala' ]
sourceSets.main.java.srcDirs = []
sourceSets.test.java.srcDirs = [ ]

def osName = org.gradle.internal.os.OperatingSystem.current()

dependencies {
  provided 'org.scala-lang:scala-library:' + scalaVersion
  provided 'org.scala-lang:scala-reflect:' + scalaVersion
  provided 'org.scala-lang:scala-compiler:' + scalaVersion

  // always use stock spark so that snappy extensions don't get accidently
  // included here in snappy-core code.
  provided "org.apache.spark:spark-core_${scalaBinaryVersion}:${sparkVersion}"
  provided "org.apache.spark:spark-catalyst_${scalaBinaryVersion}:${sparkVersion}"
  provided "org.apache.spark:spark-sql_${scalaBinaryVersion}:${sparkVersion}"
  provided "org.apache.spark:spark-hive_${scalaBinaryVersion}:${sparkVersion}"
  provided "org.apache.spark:spark-streaming_${scalaBinaryVersion}:${sparkVersion}"
  provided "org.apache.spark:spark-streaming-kafka-0-8_${scalaBinaryVersion}:${sparkVersion}"
  //provided "org.apache.spark:spark-streaming-kafka-0-10_${scalaBinaryVersion}:${sparkVersion}"
  provided "org.apache.spark:spark-mllib_${scalaBinaryVersion}:${sparkVersion}"
  provided "org.eclipse.jetty:jetty-servlet:${jettyVersion}"

  if (new File(rootDir, 'store/build.gradle').exists()) {
    compile project(':snappy-store:gemfirexd-client')
    compile project(':snappy-store:gemfirexd-core')
    compile project(':snappy-store:gemfirexd-tools')
    testCompile project(path: ':snappy-store:gemfirexd-tools', configuration: 'testOutput')
  } else {
    compile group: 'io.snappydata', name: 'snappydata-store-client', version: snappyStoreVersion
    compile group: 'io.snappydata', name: 'snappydata-store-core', version: snappyStoreVersion
    compile group: 'io.snappydata', name: 'snappydata-store-tools', version: snappyStoreVersion
    testCompile group: 'io.snappydata', name: 'snappydata-store-tools', version: snappyStoreVersion, classifier: 'tests'
  }

  compile("org.parboiled:parboiled_${scalaBinaryVersion}:2.1.3") {
    exclude(group: 'org.scala-lang', module: 'scala-library')
    exclude(group: 'org.scala-lang', module: 'scala-reflect')
    exclude(group: 'org.scala-lang', module: 'scala-compiler')
  }
  compile 'org.apache.tomcat:tomcat-jdbc:8.5.4'
  compile 'com.zaxxer:HikariCP:2.4.7'
  //compile 'org.spark-project:dstream-twitter_2.11:0.1.0'
  compile 'org.twitter4j:twitter4j-stream:4.0.4'
  provided 'com.rabbitmq:amqp-client:3.5.7'

  compile(group: 'com.databricks', name: 'spark-csv_' + scalaBinaryVersion, version: sparkCSVVersion) {
    exclude(group: 'org.scala-lang', module: 'scala-library')
    exclude(group: 'org.scala-lang', module: 'scala-reflect')
    exclude(group: 'org.scala-lang', module: 'scala-compiler')
    exclude(group: 'com.univocity', module: 'univocity-parsers')
  }

  testCompile project(':dunit')
  testCompile 'org.scala-lang:scala-actors:' + scalaVersion
  testCompile "org.scalatest:scalatest_${scalaBinaryVersion}:${scalatestVersion}"

  testCompile "org.apache.spark:spark-core_${scalaBinaryVersion}:${sparkVersion}:tests"
  testCompile "org.apache.spark:spark-streaming_${scalaBinaryVersion}:${sparkVersion}:tests"

  testRuntime "org.pegdown:pegdown:${pegdownVersion}"
}

task packageScalaDocs(type: Jar, dependsOn: scaladoc) {
  classifier = 'javadoc'
  from scaladoc
}
if (rootProject.hasProperty('enablePublish')) {
  artifacts {
    archives packageScalaDocs, packageSources
  }
}

testClasses.doLast {
  copyTestsCommonResources(buildDir)
}

scalaTest {
  dependsOn ':cleanScalaTest'
  doFirst {
    // cleanup files since scalatest plugin does not honour workingDir yet
    cleanIntermediateFiles(project.path)
  }
  doLast {
    // cleanup files since scalatest plugin does not honour workingDir yet
    cleanIntermediateFiles(project.path)
  }
}

task downloadApacheSparkDist(type: de.undercouch.gradle.tasks.download.Download) {
  outputs.files "${sparkProductDir}.tgz"

  src "http://redrockdigimark.com/apachemirror/spark/spark-${sparkVersion}/${sparkDistName}.tgz"
  dest sparkDistDir
  onlyIfNewer true

  doFirst {
    mkdir(sparkDistDir)
  }
}

task getApacheSparkDist {
  dependsOn downloadApacheSparkDist

  outputs.files "${sparkProductDir}.tgz", "${sparkProductDir}/README.md"

  doLast {
    if (osName.isWindows()) {
      copy {
        from tarTree(resources.gzip("${sparkDistDir}/${sparkDistName}.tgz"))
        into sparkDistDir
      }
    } else {
      // gradle tarTree does not preserve symlinks (GRADLE-2844)
      exec {
        executable 'tar'
        args 'xfz', "${sparkDistDir}/${sparkDistName}.tgz"
        workingDir = sparkDistDir
      }
    }
  }
}

test.dependsOn ':cleanJUnit'
dunitTest.dependsOn getApacheSparkDist
check.dependsOn test, scalaTest, dunitTest

archivesBaseName = 'snappydata-core_' + scalaBinaryVersion
shadowJar {
  zip64 = true

  mergeServiceFiles()
  exclude 'log4j.properties'

  if (rootProject.hasProperty('enablePublish')) {
    createdBy = 'SnappyData Build Team'
  } else {
    createdBy = System.getProperty('user.name')
  }
  manifest {
    attributes(
      'Manifest-Version'  : '1.0',
      'Created-By'        : createdBy,
      'Title'             : "snappydata-core_${scalaBinaryVersion}",
      'Version'           : version,
      'Vendor'            : 'SnappyData, Inc.'
    )
  }
}

// write the POM for spark-package
String sparkPackageName = "snappydata-${version}-s_${scalaBinaryVersion}"

task sparkPackagePom(dependsOn: shadowJar) << {
  file("${rootProject.buildDir}/distributions").mkdirs()
  pom {
    project {
      groupId 'SnappyDataInc'
      artifactId 'snappydata'
      version "${version}-s_${scalaBinaryVersion}"
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }
    }
    whenConfigured { p -> p.dependencies.clear() }
  }.writeTo("${rootProject.buildDir}/distributions/${sparkPackageName}.pom")
  copy {
    from "${buildDir}/libs"
    into "${rootProject.buildDir}/distributions"
    include "${shadowJar.archiveName}"
    rename { filename -> "${sparkPackageName}.jar" }
  }
}
task sparkPackage(type: Zip, dependsOn: sparkPackagePom) {
  archiveName "${sparkPackageName}.zip"
  destinationDir = file("${rootProject.buildDir}/distributions")
  outputs.upToDateWhen { false }

  from ("${rootProject.buildDir}/distributions") {
    include "${sparkPackageName}.jar"
    include "${sparkPackageName}.pom"
  }
}
